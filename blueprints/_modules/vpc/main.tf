locals {
  common_tags = {
    App = "${var.app}"
    Env = "${var.env}"
  }
}

data "aws_availability_zones" "main" {}

# ------------------------------------------------------------------------------
# [STEP 01] Create the VPC
# ------------------------------------------------------------------------------
# AWS VPC
resource "aws_vpc" "main" {
  cidr_block           = "${var.cidr_block}"
  instance_tenancy     = "${var.tenancy_type}"
  enable_dns_support   = true
  enable_dns_hostnames = false

  tags = "${merge(local.common_tags, map("Name", var.name))}"
}

# ------------------------------------------------------------------------------
# [STEP 02] Create the VPC's subnetworks
# ------------------------------------------------------------------------------
# private subnet [AZ a]
resource "aws_subnet" "private_a" {
  vpc_id            = "${aws_vpc.main.id}"
  availability_zone = "${data.aws_availability_zones.main.names[0]}"
  cidr_block        = "${cidrsubnet(aws_vpc.main.cidr_block, 3, 0)}"

  tags = "${merge(local.common_tags, map("Name", "${var.app}_private_a"))}"
}

# public subnet [AZ a]
resource "aws_subnet" "public_a" {
  vpc_id            = "${aws_vpc.main.id}"
  availability_zone = "${data.aws_availability_zones.main.names[0]}"
  cidr_block        = "${cidrsubnet(aws_vpc.main.cidr_block, 4, 2)}"
  tags              = "${merge(local.common_tags, map("Name", "${var.app}_public_a"))}"
}

# private subnet [AZ b]
resource "aws_subnet" "private_b" {
  vpc_id            = "${aws_vpc.main.id}"
  availability_zone = "${data.aws_availability_zones.main.names[1]}"
  cidr_block        = "${cidrsubnet(aws_vpc.main.cidr_block, 3, 2)}"
  tags              = "${merge(local.common_tags, map("Name", "${var.app}_private_b"))}"
}

# public subnet [AZ b]
resource "aws_subnet" "public_b" {
  vpc_id            = "${aws_vpc.main.id}"
  availability_zone = "${data.aws_availability_zones.main.names[1]}"
  cidr_block        = "${cidrsubnet(aws_vpc.main.cidr_block, 4, 6)}"
  tags              = "${merge(local.common_tags, map("Name", "${var.app}_public_b"))}"
}

# private subnet [AZ c]
resource "aws_subnet" "private_c" {
  vpc_id            = "${aws_vpc.main.id}"
  availability_zone = "${data.aws_availability_zones.main.names[2]}"
  cidr_block        = "${cidrsubnet(aws_vpc.main.cidr_block, 3, 4)}"
  tags              = "${merge(local.common_tags, map("Name", "${var.app}_private_c"))}"
}

# public subnet [AZ c]
resource "aws_subnet" "public_c" {
  vpc_id            = "${aws_vpc.main.id}"
  availability_zone = "${data.aws_availability_zones.main.names[2]}"
  cidr_block        = "${cidrsubnet(aws_vpc.main.cidr_block, 4, 10)}"
  tags              = "${merge(local.common_tags, map("Name", "${var.app}_public_c"))}"
}

# ------------------------------------------------------------------------------
# [STEP 03] Create the VPC's internet gateway
# ------------------------------------------------------------------------------
resource "aws_internet_gateway" "main" {
  vpc_id = "${aws_vpc.main.id}"
  tags   = "${merge(local.common_tags, map("Name", var.name))}"
}

# ------------------------------------------------------------------------------
# [STEP 04] Create an elastic IP address for the VPC's NAT gateway
# ------------------------------------------------------------------------------
resource "aws_eip" "main" {
  vpc = true
}

# ------------------------------------------------------------------------------
# [STEP 05] Create the VPC's main NAT gateway
# ------------------------------------------------------------------------------
resource "aws_nat_gateway" "main" {
  # declare artificial dependency on the internet gateway resource
  depends_on = ["aws_internet_gateway.main"]

  # the NAT requires an elastic IP for rerouting requests to the private subnets
  allocation_id = "${aws_eip.main.id}"

  # the NAT has to sit in a public subnet; we'll put it in 'public_a'
  subnet_id = "${aws_subnet.public_a.id}"
}

# ------------------------------------------------------------------------------
# [STEP 06] Configure the 2 route tables for private and public routing
# ------------------------------------------------------------------------------
# Use the default route table (generated by the VPC) for the private subnets
resource "aws_default_route_table" "private" {
  default_route_table_id = "${aws_vpc.main.default_route_table_id}"

  route {
    
  }

  route {
    cidr_block = "0.0.0.0/0"

    # Point public-destined traffic from the private subnets to the NAT gateway.
    nat_gateway_id = "${aws_nat_gateway.main.id}"
  }

  tags = "${merge(local.common_tags, map("Name", "${var.name}_private"))}"
}

# Create another route table for the public subnets
resource "aws_route_table" "public" {
  vpc_id = "${aws_vpc.main.id}"

  route {
    cidr_block = "0.0.0.0/0"

    # Point traffic to the internet gateway.
    gateway_id = "${aws_internet_gateway.main.id}"
  }

  tags = "${merge(local.common_tags, map("Name", "${var.name}_public"))}"
}

# ------------------------------------------------------------------------------
# [STEP 07] Associate subnets with their respective route tables
# ------------------------------------------------------------------------------
resource "aws_route_table_association" "private_a" {
  subnet_id      = "${aws_subnet.private_a.id}"
  route_table_id = "${aws_default_route_table.private.id}"
}

resource "aws_route_table_association" "private_b" {
  subnet_id      = "${aws_subnet.private_b.id}"
  route_table_id = "${aws_default_route_table.private.id}"
}

resource "aws_route_table_association" "private_c" {
  subnet_id      = "${aws_subnet.private_c.id}"
  route_table_id = "${aws_default_route_table.private.id}"
}

resource "aws_route_table_association" "public_a" {
  subnet_id      = "${aws_subnet.public_a.id}"
  route_table_id = "${aws_route_table.public.id}"
}

resource "aws_route_table_association" "public_b" {
  subnet_id      = "${aws_subnet.public_b.id}"
  route_table_id = "${aws_route_table.public.id}"
}

resource "aws_route_table_association" "public_c" {
  subnet_id      = "${aws_subnet.public_c.id}"
  route_table_id = "${aws_route_table.public.id}"
}

# ------------------------------------------------------------------------------
# [STEP 08] Configure VPC's security groups (firewall)
# ------------------------------------------------------------------------------
# Bring the default security group (generated by the VPC) under our control.


# ------------------------------------------------------------------------------
# [STEP 09] Configure VPC's network ACL
# ------------------------------------------------------------------------------

# Bring the default network ACL (generated by the VPC) under our control.
resource "aws_default_network_acl" "main" {
  default_network_acl_id = "${aws_vpc.main.default_network_acl_id}"

  ingress {
    protocol   = -1
    rule_no    = 100
    action     = "allow"
    cidr_block = "0.0.0.0/0"
    from_port  = 0
    to_port    = 0
  }

  egress {
    protocol   = -1
    rule_no    = 100
    action     = "allow"
    cidr_block = "0.0.0.0/0"
    from_port  = 0
    to_port    = 0
  }

  tags = "${merge(local.common_tags, map("Name", var.name))}"
}
